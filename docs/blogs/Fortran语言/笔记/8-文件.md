# 08-文件

## 文件的操作

### 概述

- 打开文件 open
- 读写文件 read write
- 关闭文件 close
- 查询文件 inquire
- 其它命令

### open的使用

open ：打开文件 （关联一个文件与通道）

open的参数

```c
open( [unit=]number [,file='filename'] [,form='...'] [,status='...'] [,access='...']
[,recl=length] [,err=label] [,iostat=iostat] [,blank='...'] [,position='...']
[,action=action] [,pad='...'] [,delim='...'])
```

- `[unit=]number`: 指定文件的逻辑单元号（unit number）。
	- 其中，unit= 可以进行省略
	- number 必须为正整数
	- number 可以使用变量或常量进行赋值
	- number 应该尽量避免使用5和6（<font color="##c00000">5默认是键盘输入，6默认是屏幕输出</font>）
- `[,file='filename']`: 指定文件的名称。
	- 文件名要符合操作系统的规定
	- 避免使用中文字符
- `[,form='...']`: 指定文件的格式。
	- `formatted` 表示文件使用**文本文件**格式来保存
	- `unformatted `表示文件使用**二进制文件**格式来保存
- `[,status='...']`: 指定文件的状态。
	- new：新建文件
	- old：打开已有文件
	- replace： 替换已有文件或新建文件
	- scratch：打开一个暂存盘，不需要指定文件名，程序结束后自动删除
	- unknown：由编译器自定义，通常为新建文件或打开已有文件（如果有写入，会覆盖原有的内容）
- `[,access='...']`: 指定文件的访问方式 ^69cf05
	- `'sequential'` 表示顺序访问 **默认值**
	- `'direct'` 表示直接访问 （可指定任意位置）
- `[,recl=length]`: 顺序读取文件时，设置一次可以读写多大容量的数据
	- 直接读取文件时，设置文件中每个模块单元的分区长度
	- 单位
		- 在文本格式下，为一个字符（1B）
		- 在二进制格式下，由编译器决定（1B或4B）
- `[,err=label]`: 设置当文件打开发生错误时，程序会跳到代码为lable的行继续执行。
	- 1-99999范围内的整数
- `[,iostat=var]`: 返回文件操作的状态码。 ^ccb47a
	- 返回一个值给变量var，用来说明文件打卡的状态
	- $var>0$：表示文件读取操作发生错误
	- $var=0$：表示读取操作正常
	- $var<0$：表示文件终了
- `[,blank='...']`: 设置文件输入数字时，当所设置的格式字段中有空格存在时，所代表的意义
	- `null`  ：表示空格代表没有东西（<font color="##e36c09">默认</font>）
	- `zero` ：空格自动以0代替
- `[,position='...']`: 指定文件打开时的读写初始位置
	- `rewind` 表示将文件指针重置到开头
	- `asis` 表示不特别指定，通常在文件开头（<font color="##e36c09">默认</font>）
	- `append` 表示移到文件结尾（多用于续写文件）
- `[,action=...]`: 设置打开文件的读写权限，避免误写 
	- readwrite ：表示可读取和写入（<font color="##e36c09">默认</font>）
	- read：表示只能读取
	- write：表示只能写入
- `[,pad='...']`: 确定格式化输入时，前面不足的字段是否要自动以空格进行填充
	- yes 
	- no
- `[,delim='...']`: 指定字符串之间的分隔符。
	- none ：只输出字符串的内容（<font color="##e36c09">默认</font>）
	- quote：输出字符串会在前后加上双引号
	- apostrophe: 输出字符串时会在前后加上单引号

### read 和 write的使用

```c
read/write	([unit=]number [,fmt=format] [,rec=record] [,iostat=stat]	
			[,err=errlabel]	[,end=endlabel]	[,advance=advance] [,size=size])
```

- `([unit=]number`: 指定文件的逻辑单元号（unit number）。
- `[,fmt=format]`: 指定格式控制。
- `[,rec=record]`: 在直接读取文件中，设置读写的文件模块位置。
- `[,iostat=stat]`: 返回读取或写入操作的状态码。
	- ![[8-文件##^ccb47a]]
- `[,err=errlabel]`: 表示在读写过程中发生错误时，会跳到代码为errlable的行继续执行。
- `[,end=endlabel]`: 表示读写到文件末尾时，跳到代码为endlable的行继续执行。
- `[,advance=advance]`
	- 设置在文本格式下的顺序文件中，每一次的读写命令完成后，读写位置是否自动向下移动一行。
		- yes （<font color="##e36c09">默认</font>）
		- no
- `[,size=count]`: 
	- 当advance='no'时，返回此次输入/输出的字符数目给整型变量count

### close的使用
`
```c
close ([unit=] number [,status=string] [,err=errlabel] [,iostat=iostat])
```

- status : 决定文件关闭后的操作
	- keep 或 save ：表示在文件关闭后，保留该文件（<font color="##e36c09">默认</font>）
	- delete ： 表示该文件关闭后，删除该文件

### 查询文件的状态

注意📢：可以采用通道号或文件名来查询文件，但是，以上两种方式只能选择一种进行使用

```c
inquire ([unit=number] [,file='filename'] [,iostat=stat] [,err=label] [,exist=exist]
[,opened=opened] [,sequential=sequential] [,direct=direct] [,form=form]	[,formatted=formatted]	
[,unformatted=unformatted]	[,recl=recl])
```

- `exist`：检查文件是否存在
	- 返回一个布尔值给逻辑变量`exist`
	- 真值，表示文件存在
	- 假值，表示文件不存在
- `opened` : 检查文件是否已经使用open命令打开
- `number`：由文件名来查询该文件的通道号
- `named`：查询文件是否取了名字
- `access`：检查文件的读取格式，返回一个字符串给字符型变量access
	- `'sequential'` 表示顺序访问 **默认值**
	- `'direct'` 表示直接访问 （可指定任意位置）
	- `undefined`:表示没有定义
	- ![[8-文件##^69cf05]]
- `sequential`：查看文件是否使用顺序格式
- `direct`：查看文件是否使用直接格式
- `form` 查看文件的保存方法
- `unformatted`：查看文件是否为二进制文件
- `formatted` ： 查看文件是否为文本文件

### 其它文件运行命令

- `backspace(unit=number,err=lable,iostat=stat)`
	- 把文件的读写位置退回一步
- `endfile(unit=number,err=errlable,iostat=stat)`
	- 把文件的读写位置设为文件末尾
- `rewind(unit=number,err=lable,iostat=stat)`
	- 把文件的读写位置倒回文件开头

## 顺序文件的操作

顺序文件在读写时，不能任意赋值到文件的某个位置读写数据，只能从头开始一步步向下进行。

改变文件读写位 置时，只能一步步地进退，或是直接移回文件开头。

例如：文本文件

```c
program main
	implicit none
	character (len=79)	::	filename
	character (len=79)	::	buffer
	integer,parameter :: fileid =	10
	integer ::	status	=	0
	logical alive
	
	write (*,*)	"Filename:"
	read (*,"(A79)")filename
	inquire (file=filename,	exist=alive)
	if (alive)	then
		open (unit=fileid,	file=filename,	access="sequential",	status="old")
		do while (.true.)
			read (unit=fileid,fmt="(A79)",iostat=status) buffer
			if (status/=0)	exit !没有数据就跳出循环
			write (*,"(A79)")buffer
		end do
	else
		write (*,*)	 trim(filename),"doesn't exist."
	end if
	
	stop
end
```

![[读文件示例.excalidraw|center|800]]

## 直接访问文件的操作

直接访问文件，是把文件的空间、内容事先分区成好几个同样大小的模块，这些模块会自动按顺序编号。

读写文件是，要先赋值文件读写位置在第几个模块，再来进行读写的工作。

直接访问文件可以任意到文件的任一个模块来读写。

```c
program main
	implicit none
	integer,parameter ::	fileid =	10
	character (len=20)	::	filename	=	"list.txt"
	integer player
	real hit
	integer error
	logical alive
	inquire (file=filename,	exist=alive)
	if (.not.alive)	then
		write(*,*)	trim (filename),"doesn't exist."
		stop
	end if
	open (unit=fileid,file=filename,access="direct",form="formatted",recl=6,status="old")
	do while (.true.)
		write(*,"('查询第几棒?')")
		read (*,*)	player
		read (fileid,	fmt="(F4.2)",	rec=player,	IOSTAT=error)	hit
		if (error/=0)	exit
		write(*,"('打击率:'F4.2)")	hit
	end do
	close (fileid)
	stop
end program
```

## 二进制文件的读取

```c
program	main
	implicit none
	integer,parameter ::fileid = 10
	character (len=20)	::	filename = "list.bin"
	integer player
	real ::	hit(9) = (/3.2,2.8,3.3,3.2,2.9,2.7,2.2,2.3,1.9/)
	open (unit=fileid,file=filename,access="direct",recl=4,	status="replace") 
	do player=1,9	
		write (fileid,rec=player)	hit(player)
	end do
	close (fileid)
	stop
end program main
```

